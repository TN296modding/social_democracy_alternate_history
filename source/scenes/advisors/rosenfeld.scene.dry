title: Kurt Rosenfeld
is-pinned-card: true
tags: advisor, left
card-image: img/portraits/RosenfeldKurt.jpg
view-if: rosenfeld_advisor = 1
new-page: true 

= Kurt Rosenfeld

Rosenfeld works as a lawyer in Berlin who defends left-wing individuals and causes. He favors cooperation with the KPD and a path of class struggle for the SPD.

- @building_the_left
- @kpd_cooperation
- @against_toleration
- @judiciary_
- @root: Return to main

@building_the_left
title: Building the Left
subtitle: Push the SPD to the left.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer = 0
on-arrival: advisor_action_timer = 0; left_strength += 60; left_dissent -= 80; reformist_dissent += 2

We have increased the strength of the left faction with the SPD.

@kpd_cooperation
title: Cooperation with the KPD
subtitle: Push the party to be more friendly towards the KPD.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
choose-if: advisor_action_timer = 0
on-arrival: advisor_action_timer = 0; left_strength += 50; kpd_relation += 40; reformist_dissent += 2; communist_coalition += 10 if kpd_cooperation_seen <= 1; kpd_cooperation_seen += 1; communist_coalition += 10 if kpd_cooperation_seen >= 1

Through his work, Rosenfeld has defended individual KPD members; this should make some connections possible.

@against_toleration
title: Against toleration
view-if: not constructive_vonc
subtitle: Break our current toleration arrangement, and bring down the government via a no-confidence vote.
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?] [? if spd_toleration == 0 : The SPD is not currently in toleration. ?]
choose-if: advisor_action_timer == 0 and spd_toleration
on-arrival: advisor_action_timer = 0; next_election_time = time + 3 if next_election_time > time+3; next_election_month = month + 3 if next_election_time >= time + 3; next_election_year = year if next_election_time >= time + 3; next_election_year += 1 if next_election_month > 12; next_election_month -= 12 if next_election_month > 12; spd_toleration = 0; left_strength += 8; pro_republic -= 4; new_middle_spd -= 3; rural_spd -= 3; old_middle_spd -= 3; catholics_spd -= 3; reformist_dissent += 8

We have grown tired of the burdens of toleration, and have called a vote of no confidence in the current minority government.

New elections have been set for [+ next_election_month : month +] [+ next_election_year +].

@judiciary_
title: Judicial Affairs
subtitle: As a lawyer, Rosenfeld has some influence in the Justice ministry
choose-if: advisor_action_timer = 0 and spd_in_government and justice_minister_party = "SPD"
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?][? if not spd_in_government : The SPD is not currently in government. ?][? if justice_minister_party != "SPD" : We do not have the Justice Ministry. ?]
on-arrival: advisor_action_timer = 0; judiciary_timer -= 6; last_advisor_action = 1
go-to: judiciary
