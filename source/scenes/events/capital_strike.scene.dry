title: Capital Strike
subtitle: Germany's industries are idling...
view-if: (capital_strike_progress >= 10 or budget <= -5) and spd_in_government
new-page: true
max-visits: 1
on-arrival: unemployed += 50; workers_spd*= 7; new_middle_spd -= 15; old_middle_spd -= 15; unemployed_spd*=7; rural_spd -= 15; pro_republic -= 15; capital_strike_seen = 1; z_relation -= 100; dvp_relation -= 100; workers_nsdap += 80; unemployed_nsdap += 100; new_middle_nsdap += 15; old_middle_nsdap += 15; rural_nsdap += 15; economic_growth -= 40;
tags: event

= Capital Strike

Our policies have greatly angered the capitalists. Investment has fallen, and factories are idling, preventing the workers from fulfilling their jobs. Meanwhile, the bourgeois press is a constant chorus of venomous invective directed at us and our economic policies.

What can we do?

- @seize
- @capital_controls
- @propaganda
- @nothing

@seize
title: Empower workers to seize the factories!
unavailable-subtitle: [? if left_strength <= reformist_strength + neorevisionist_strength : The bulk of the party does not support this radical action. ?] [? if judicial_reform < 3 : The judiciary would oppose this action. ?] [? if rb_militancy < 0.25 : If we increased the strength of our paramilitary forces, perhaps we could do this. ?] [? if works_councils < 1 : We need the support of the works councils first. ?]
choose-if: left_strength > reformist_strength + neorevisionist_strength and (judicial_reform >= 3 or rb_militancy >= 0.25) and works_councils >= 1
on-arrival: nationalization_progress += 10; coup_progress += 6; unemployed -= 20; unemployed -= 10 if works_councils >= 3; works_program += 1; workers_spd += 100; unemployed_spd += 100; coalition_dissent += 20; dvp_relation -= 40; workers_nsdap -= 80; unemployed_nsdap -= 80; left_strength += 150; reformist_dissent += 100; left_dissent -= 100; factory_takeovers += 10; economic_growth += 20;

If the capitalists are going to attack us, then we must hit them back. We have empowered the workers to seize and take full control of the factories that the capitalists have idled. The fascists, of course, call us thieves and looters.


@capital_controls
title: Enact capital controls to lessen the impact.
choose-if: judicial_reform >= 2
unavailable-subtitle: The judiciary would never allow this.
on-arrival: unemployed -= 15; workers_spd += 60; unemployed_spd += 60; economic_growth += 20;

Capital controls might blunt some of the worst economic components of the capital strike.



@propaganda
title: Launch a propaganda campaign blaming the capitalists for the crisis.
subtitle: -1 resources
choose-if: (radio >= 2 or commercialized_media >= 3) and resources >= 1
unavailable-subtitle: [? if radio < 2 and commercialized_media < 3 : We have not built up our media enough. ?] [? if resources < 1: We do not have enough resources. ?]
on-arrival: workers_spd += 70; new_middle_spd += 60; unemployed_spd += 60; pro_republic += 50; resources -= 1

We launch a counter-propaganda campaign to blame the economic crisis on the capitalists, using all of our media channels to do so. Because of our existing media framework, this is moderately successful in directing popular criticisms towards the capitalists and away from us.


@nothing
title: There is nothing we can do.

There is nothing we can do that is within constitutional and legal bounds.
